/*
 * generated by Xtext 2.25.0
 */
package org.xtext.example.mydsl.validation

import org.eclipse.xtext.validation.Check
import org.xtext.example.mydsl.projectDSL.Controller
import java.util.ArrayList
import org.xtext.example.mydsl.projectDSL.ProjectDSLPackage.Literals
import org.xtext.example.mydsl.projectDSL.Entity
import org.xtext.example.mydsl.projectDSL.Parameter
import org.xtext.example.mydsl.projectDSL.RestAPI
import org.xtext.example.mydsl.projectDSL.impl.EntityImpl
import org.xtext.example.mydsl.projectDSL.impl.ControllerImpl

/**
 * This class contains custom validation rules. 
 *
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#validation
 */
class ProjectDSLValidator extends AbstractProjectDSLValidator {
	
	@Check
	def checkMakeOperations(Controller c) {
		val endpointNames = new ArrayList<String>
		// We add the names of each of the endpoints.
		c.endpoint.forEach[
			if(endpointNames.contains(it.endpoint.name)) {
				error (it.endpoint.name + " has already been created as an endpoint in this controller, only one can exist", it, Literals.ENDPOINT__ENDPOINT)
			} else {
				endpointNames.add(it.endpoint.name)
			}
		]
	}
	
	@Check
	def checkDuplicateUsesStatement(Controller c) {
		val entitiesUsed = new ArrayList<String>
		c.base.forEach[
			if(entitiesUsed.contains(it.name)){
				error (it.name + " has already been mentioned after the uses, duplication is not allowed!", c, Literals.CONTROLLER__BASE)
			} else {
				entitiesUsed.add(it.name)
			}
		]
	}
	
	@Check
	def checkDuplicateParameter(Entity e) {
		val entityParameterNames = new ArrayList<String>
		e.parameters.forEach[
			if(entityParameterNames.contains(it.name)) {
				error (it.name + " already exists! Duplication of parameters is not allowed", it, Literals.PARAMETER__NAME)
			} else {
				entityParameterNames.add(it.name)
			}
		]
	}
    
    @Check
    def checkDuplicateCRUD(Parameter parameter){
    	for(var i = 0; i < parameter.type.length; i++){
            for(var j = 0; j < parameter.type.length; j++){
                if(i != j && parameter.type.get(i) == parameter.type.get(j)){
                    error("Parameters can only contain one of " + parameter.type.get(i) + " operation, duplication is not allowed!", parameter, Literals.PARAMETER__NAME)
                }
            }
        }
    }
    
    @Check
    def checkDuplicateEntitiesOrControllers(RestAPI api){
    	val entityNames = new ArrayList<String>
    	val controllerNames = new ArrayList<String>
    	api.declarations.forEach[
    		if(it.class == EntityImpl && !entityNames.contains(it.name)){
    			entityNames.add(it.name)
    		}else if(it.class == ControllerImpl && !controllerNames.contains(it.name)){
    			controllerNames.add(it.name)
    		}else{
    			error(it.name + " already exists, duplication is not allowed!", it, null)
    		}
    	]
    }
	
}
